#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда    
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры 

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) 
	
	Для каждого Строка Из ОтпускаСотрудников Цикл   
		
		Если ЗначениеЗаполнено(Строка.ДатаНачала) И ЗначениеЗаполнено(Строка.ДатаОкончания) Тогда 
			Если Год(Строка.ДатаНачала) <> Год(Год) Или Год(Строка.ДатаОкончания) <> Год(Год) Тогда
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Строка №%1 год документа отличается от года в строке.", Строка.НомерСтроки));
				Отказ = Истина;
			КонецЕсли;
			
			Если Строка.ДатаНачала > Строка.ДатаОкончания Тогда  
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Строка №%1 неверно заполнен период отпуска", Строка.НомерСтроки)); 
				Отказ = Истина; 
			КонецЕсли; 
		Иначе 
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Строка №%1 неверно задан период отпуска", Строка.НомерСтроки)); 
			Отказ = Истина; 
		КонецЕсли; 
	КонецЦикла;  
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ВКМ_УчетДнейОтпусков.Записывать = Истина;
		
	Запрос = Новый Запрос;
	Запрос.Текст = ("ВЫБРАТЬ
	                |	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник КАК Сотрудник,
	                |	ЕСТЬNULL(СУММА(РАЗНОСТЬДАТ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала, ДОБАВИТЬКДАТЕ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания, ДЕНЬ, 1), ДЕНЬ)), 0) КАК СуммаДнейОтпуска,
	                |	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка.Год КАК Период
	                |ИЗ
	                |	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковОтпускаСотрудников
	                |ГДЕ
	                |	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка = &Ссылка
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник,
	                |	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка.Год");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		// Движения по регистру ВКМ_УчетДнейОтпусков
		Движение = Движения.ВКМ_УчетДнейОтпусков.Добавить();	
		Движение.Период = Выборка.Период;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.ВКМ_УчетДнейОтпусков.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
