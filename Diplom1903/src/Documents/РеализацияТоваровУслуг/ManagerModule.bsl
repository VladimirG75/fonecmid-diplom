
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавление команды Создать на основании
//
// Параметры:
// КомандыСозданияНаОсновании - ТаблицаЗначений
// 
// Возвращаемое значение:
//  - СтрокаТаблицыЗначений
//  - Неопределено
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

//{{Доработка ВКМ_АбонентскоеОбслуживание: Добавление команды печати Акта об оказанных услугах 
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
	// Акт оказанных услуг
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОказанныхУслуг";
	КомандаПечати.Представление = НСтр("ru = 'Акт оказанных услуг'");
	КомандаПечати.Порядок = 10;
	
КонецПроцедуры
//{{Доработка ВКМ_АбонентскоеОбслуживание

//{{Доработка ВКМ_АбонентскоеОбслуживание: Добавление печатной формы Акта об оказанных услугах
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
			
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОказанныхУслуг");
	
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАктаОказанныхУслуг(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт оказанных услуг'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОказанныхУслуг";
	КонецЕсли;
	
КонецПроцедуры
//{{Доработка ВКМ_АбонентскоеОбслуживание
#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

//{{Доработка ВКМ_АбонентскоеОбслуживание: Добавление функционала по печати Акта об оказанных услугах
Функция ПечатьАктаОказанныхУслуг(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "АктОказанныхУслуг";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОказанныхУслуг");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиОсновнуюТаблицуАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиПодвалАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Номер КАК Номер,
	               |	РеализацияТоваровУслуг.Дата КАК Дата,
	               |	РеализацияТоваровУслуг.Организация КАК Организация,
	               |	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	               |	РеализацияТоваровУслуг.Договор КАК Договор,
	               |	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	               |	РеализацияТоваровУслуг.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги,
	               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции
	
Процедура ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовокДокумента = Макет.ПолучитьОбласть("ЗаголовокДокумента");
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер));
	ДанныеПечати.Вставить("ДатаДокумента", Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ДанныеДокументов.Ссылка, 1, 120);
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
		|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
		КартинкаQRКода = Новый Картинка(ДанныеQRКода);
		ОбластьЗаголовокДокумента.Рисунки.QRКод.Картинка = КартинкаQRКода;
	КонецЕсли;
	
	ДанныеПечати.Вставить("Организация", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Контрагент", ДанныеДокументов.Контрагент);
	ДанныеПечати.Вставить("Договор", ДанныеДокументов.Договор);
	
	ОбластьЗаголовокДокумента.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьЗаголовокДокумента);
	
КонецПроцедуры

Процедура ВывестиОсновнуюТаблицуАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	СуммаУслугИтого = 0;
		
	ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();
	
	Пока ВыборкаУслуги.Следующий() Цикл
		ОбластьСтрокаТаблицы.Параметры.Заполнить(ВыборкаУслуги);
		ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		СуммаУслугИтого = СуммаУслугИтого + ВыборкаУслуги.Сумма;
	КонецЦикла;
	
	ОбластьИтого = Макет.ПолучитьОбласть("Итого"); 
	ОбластьСуммаПрописью = Макет.ПолучитьОбласть("СуммаПрописью");
	
	ПараметрыИсчисления = "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2";
	ФорматнаяСтрока = "Л = ru_RU; ДП = Истина";
	
	СуммаПрописью = ЧислоПрописью(СуммаУслугИтого, ФорматнаяСтрока, ПараметрыИсчисления);
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Всего", СуммаУслугИтого);
	ДанныеПечати.Вставить("Сумма", СуммаУслугИтого);
	ДанныеПечати.Вставить("СуммаПрописью", СуммаПрописью);
	
	ОбластьИтого.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьИтого); 
	
	ОбластьСуммаПрописью.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);

КонецПроцедуры

Процедура ВывестиПодвалАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
КонецПроцедуры
//{{Доработка ВКМ_АбонентскоеОбслуживание
#КонецОбласти

#КонецЕсли