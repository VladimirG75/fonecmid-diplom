
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Добавление новых реквизитов и группы ГруппаПериод на форму элемента справочника
	Если Элементы.Найти("АбонентскаяПлата") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("АбонентскаяПлата", Тип("ПолеФормы"));		
		НовыйЭлемент.ПутьКДанным = "Объект.ВКМ_АбонентскаяПлата";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок = "Абонентская плата";
	КонецЕсли;
		
	Если Элементы.Найти("СтоимостьЧасаРаботы") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("СтоимостьЧасаРаботы", Тип("ПолеФормы"));		
		НовыйЭлемент.ПутьКДанным = "Объект.ВКМ_СтоимостьЧасаРаботы";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок = "Стоимость часа работы специалиста";
	КонецЕсли;
	
	ГруппаПериод = Элементы.Добавить("ГруппаПериод", Тип("ГруппаФормы"),ЭтотОбъект);
	ГруппаПериод.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПериод.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаПериод.ОтображатьЗаголовок = Ложь;
	ГруппаПериод.Отображение = ОтображениеОбычнойГруппы.Нет;

		
	Если Элементы.Найти("ДатаНачалаДействияДоговора") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("ДатаНачалаДействияДоговора", Тип("ПолеФормы"), ГруппаПериод);		
		НовыйЭлемент.ПутьКДанным = "Объект.ВКМ_ДатаНачалаДействияДоговора";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок = "Период действия договора с";
	КонецЕсли;
		
	Если Элементы.Найти("ДатаОкончанияДействияДоговора") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("ДатаОкончанияДействияДоговора", Тип("ПолеФормы"), ГруппаПериод);		
		НовыйЭлемент.ПутьКДанным = "Объект.ВКМ_ДатаОкончанияДействияДоговора";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок = "по";
	КонецЕсли;
	
	Элементы.ВидДоговора.УстановитьДействие("ПриИзменении", "ВидДоговораПриИзменении");
	
	ВывестиЭлементыНаФорму(ЭтотОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	
	ВывестиЭлементыНаФорму(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиЭлементыНаФорму(Форма)
	
	//Настройка условия видимости новых элементов формы
	Если Форма.Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание") Тогда
		
		Форма.Элементы.АбонентскаяПлата.Видимость = Истина;
		Форма.Элементы.СтоимостьЧасаРаботы.Видимость = Истина;
		Форма.Элементы.ДатаНачалаДействияДоговора.Видимость = Истина;
		Форма.Элементы.ДатаОкончанияДействияДоговора.Видимость = Истина;
		
	Иначе
		
		Форма.Элементы.АбонентскаяПлата.Видимость = Ложь;
		Форма.Элементы.СтоимостьЧасаРаботы.Видимость = Ложь;
		Форма.Элементы.ДатаНачалаДействияДоговора.Видимость = Ложь;
		Форма.Элементы.ДатаОкончанияДействияДоговора.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

