#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Объект.СписокРеализаций.Очистить(); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Заполните период создания актов");
		Возврат;
	КонецЕсли;
			
	ДлительнаяОперация = НачатьВыполнениеНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Вставить("ВыводитьСообщения", Истина);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НачатьВыполнениеНаСервере()
	
	 ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, "Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьСписокНаСервере",
	 Объект.Период.ДатаНачала, Объект.Период.ДатаОкончания);
	 
	 Возврат ДлительнаяОперация;

КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивДоговоров = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если МассивДоговоров.Количество() = 0 Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Документы не созданы. В указанном периоде документы Реализации абонентского обслуживания уже созданы или нет актуальных договоров обслуживания");
		
	КонецЕсли;
	
	ЗаписатьРезультат(МассивДоговоров);
    
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьРезультат(МассивДоговоров)
	
	Объект.СписокРеализаций.Очистить();
	
	Для Каждого Строка Из МассивДоговоров Цикл 
		
		СтрокаТабФормы = Объект.СписокРеализаций.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабФормы, Строка);
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон("Создан документ %1", СтрокаТабФормы.Реализация));
		
	КонецЦикла;

КонецПроцедуры  

#КонецОбласти
 
