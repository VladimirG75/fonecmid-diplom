#Область ПрограммныйИнтерфейс

Процедура ОбработатьСообщенияТелеграмм() Экспорт
	
	ТокенТелеграммБота = Константы.ВКМ_ТокенУправленияТГБотом.Получить();
	
	Если ПустаяСтрока(ТокенТелеграммБота) Тогда
		
		ЗаписьЖурналаРегистрации("Отправка сообщений в телеграмм", УровеньЖурналаРегистрации.Ошибка,,, 
		"Телеграмм токен не установлен. Отправка сообщений невозможна");
		Возврат;
		
	КонецЕсли;
	
	ИдентификаторЧата = Константы.ВКМ_ИдентификаторГруппы.Получить();
	
	Если ПустаяСтрока(ИдентификаторЧата) Тогда
		
		ЗаписьЖурналаРегистрации("Отправка сообщений в телеграмм", УровеньЖурналаРегистрации.Ошибка,,, 
		"Идентификатор чата телеграмм не установлен. Отправка сообщений невозможна");
		Возврат;
		
	КонецЕсли;
	
	ВыборкаСообщений = ПолучитьСообщенияДляОтправки();
	
	Пока ВыборкаСообщений.Следующий() Цикл
		
		Ответ = ОтправитьСообщениеВТелеграмм(ВыборкаСообщений.ТекстСообщения, ТокенТелеграммБота, ИдентификаторЧата);
		
		Если Ответ.КодСостояния = 200 Тогда
			
			СообщениеОбъект = ВыборкаСообщений.Ссылка.ПолучитьОбъект();
			СообщениеОбъект.Удалить();
			
		Иначе
			
			ИнформацияОбОшибке = Ответ.ПолучитьТелоКакСтроку();
			
			ЗаписьЖурналаРегистрации("Отправка сообщений в телеграмм", УровеньЖурналаРегистрации.Ошибка,,, 
			ИнформацияОбОшибке);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


Функция ОтправитьСообщениеВТелеграмм(ТекстСообщения, ТокенТелеграммБота, ИдентификаторЧата)

	ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
    Сервер = "api.telegram.org";
	
	АдресТокена = СтрШаблон("%1%2", "bot", ТокенТелеграммБота);
	ЧастиАдреса = Новый Массив;
	ЧастиАдреса.Добавить(АдресТокена);
	ЧастиАдреса.Добавить("sendMessage");
	
	Путь = СтрСоединить(ЧастиАдреса, "/");
	
	Соединение = Новый HTTPСоединение(Сервер,,,,,,ЗащищенноеСоединение);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("chat_id", ИдентификаторЧата);
	ДанныеЗапроса.Вставить("text", ТекстСообщения);
	
	Запрос = Новый HTTPЗапрос(Путь, Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрокаJSON(ДанныеЗапроса));
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	
	Возврат Ответ;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтрокаJSON(ОбъектJSON)
	
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(ПараметрыЗаписи);
	ЗаписатьJSON(Запись, ОбъектJSON);
	
	Возврат Запись.Закрыть();
	
КонецФункции

Функция ПолучитьСообщенияДляОтправки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграммБоту.Ссылка КАК Ссылка,
		|	ВКМ_УведомленияТелеграммБоту.Текст КАК ТекстСообщения
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграммБоту КАК ВКМ_УведомленияТелеграммБоту";
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

#КонецОбласти 

 